---
title: Electron打包
data: 2024-05-11
---

Electron是用TS编写windows应用的框架。作为一个windows应用，是很适合打包成一个安装文件的。

Electron提供了多个打包方式，本文不过多介绍，在本文中，使用electron-builder进行打包，官网地址：https://www.electron.build/index.html

# 项目构建

首先构建一个示例项目，参考：

https://www.electronjs.org/zh/docs/latest/tutorial/tutorial-first-app

# package.json配置

1. 安装依赖

   ```
   npm install electron-builder
   ```

2. 配置

   ```json
   {
     "name": "my-electron-app",
     "version": "1.0.0",
     "description": "",
     "main": "index.js",
     "scripts": {
       "start": "electron .",
       "test": "echo \"Error: no test specified\" && exit 1",
       "make": "electron-builder"													// 添加打包命令： npm run make
     },
     "author": "",
     "license": "ISC",
     "devDependencies": {
       "electron": "^30.0.3"
     },
     
     // electron-builder 配置
     "build": {
       "productName": "hello-electron",
       "appId": "com.harry.helloElectron",
       "copyright": "harry",
       "directories": {
         "output": "build"
       },
       // 可选择平台，具体见：
       // https://www.electron.build/
       "win": {
         "target": [
           {
             "target": "nsis"
           }
         ],
         "artifactName": "${productName}-setup-${version}.${ext}"
       },
       
       // windows 通过 nsis 打包安装文件，配置:
       // https://www.electron.build/configuration/nsis.html
       "nsis": {
         "oneClick": false,
         "allowElevation": true,
         "allowToChangeInstallationDirectory": true,
         "createDesktopShortcut": true,
         "createStartMenuShortcut": true,
         "shortcutName": "helloElectron"
       }
     }
   }
   ```

# 自定义安装页面（win）

electron-builder通过nsis打包安装文件。NSIS（Nullsoft Scriptable Install System）是一个用于创建 Windows 安装程序的开源脚本系统。

通过上面的配置，已经有一套已经封装好的安装页面了：选择用户->选择安装路径->安装。

但有时候，我们可能需要自定义安装页面并进行一些自定义的操作。这就需要对nsis脚本做定制。

## **修改package.json**

我们在项目的根目录创建一个nsis脚本文件：build.nsh，然后在package.json里引入

```json
"nsis": {
  "oneClick": false,
  "allowElevation": true,
  "allowToChangeInstallationDirectory": true,
  "createDesktopShortcut": true,
  "createStartMenuShortcut": true,
  "shortcutName": "helloElectron",
  "include": "./build.nsh"							// 引入脚本
}
```

根据electron-builder文档，脚本引入有两种方式：include：自定义脚本. script:完全定制化脚本。

一般来说，只用第一个就够，完全重写脚步代价太大。

如果需要注册表操作，我们还可以引入guid保证项目在注册表有确定的命名:

```json
"nsis": {
  "oneClick": false,
  "allowElevation": true,
  "allowToChangeInstallationDirectory": true,
  "createDesktopShortcut": true,
  "createStartMenuShortcut": true,
  "shortcutName": "helloElectron",
  "include": "./build.nsh",
  "guid": "HelloElectron"							// 设置guid
}
```

allowToChangeInstallationDirectory选项让我们可以选择安装目录，但也绑死了页面顺序。如果我们想在选择安装目录与安装之间加一个自定义页面，那么要关掉这个页面并在nsis脚本里引入。

```json
"nsis": {
  "oneClick": false,
  "allowElevation": true,
  "allowToChangeInstallationDirectory": false,		// 改为false，或者直接删掉
  "createDesktopShortcut": true,
  "createStartMenuShortcut": true,
  "shortcutName": "helloElectron",
  "include": "./build.nsh",
  "guid": "HelloElectron"
}
```

总而言之，我们应该根据不同的需求去配置nsis。

## **脚本编写**

nsis用了一种低级脚本语言，甚至for循环都没有，在编写自定义需求的时候，还需要善用谷歌，与查询nsis官方文档：

https://nsis.sourceforge.io/Main_Page

这里记录一些笔者用到的内容：

我们假设需求是：欢迎页面->应用选择目录->数据选择目录->安装页面->安装完成。

我们已经将nsis的配置改成如下：

```
"nsis": {
  "oneClick": false,
  "perMachine": true,
  "shortcutName": "helloElectron",
  "warningsAsErrors": false,
  "include": "./build.nsh",
  "guid": "HelloElectron"
}
```

这里增加了一个"warningsAsErrors": false，以避免某些警告导致编译失败，

打开构建好的安装包，会直接进入安装页面进行安装。

**引入现代化组件**

nsis提供了一些现代化的页面，可以在脚本里直接引用：

```nsis
!include "MUI2.nsh"

# Installer pages
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE textfile
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_STARTMENU pageid variable
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

# Uninstaller pages
!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_LICENSE textfile
!insertmacro MUI_UNPAGE_COMPONENTS
!insertmacro MUI_UNPAGE_DIRECTORY
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH
```

实际上，electron-builder就是引入了这些组件，并做了一层功能化的封装，可以看electron-builder的源码：
https://github.com/electron-userland/electron-builder/blob/master/packages/app-builder-lib/templates/nsis/assistedInstaller.nsh

这个文件里就有开启了allowToChangeInstallationDirectory后的脚本。

**按顺序引入自定义页面**

根据需求，我们需要添加“欢迎页面->应用选择目录->数据选择目录”这三个目录，其中，前二者属于MUI组件，我们直接引用即可，第三个属于我们的自定义页面。

```nsis
!include "MUI2.nsh"

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_DIRECTORY
Page custom CustomFunction CustomFunctionLeave
Function CustomFunction
FunctionEnd
Function CustomFunctionLeave
FunctionEnd

UninstPage custom un.CustomFunction
Function un.CustomFunction
FunctionEnd
```

- 自定义页面格式：Page custom {function} {functionLeave}
- Page custom 固定，不可修改
- function为自定义页面要做的函数，可以增加触发函数回调，比如给按钮增加一个功能。
- functionLeave为点击下一步以后，离开这个页面后要做的函数。
- Page一定要有一个UninstPage，并且函数名要un.开头，否则报错

**页面功能定制**

这样打包好以后，页面就都有了，但是页面功能要做一些定制：

1. 安装路径自动添加应用目录

   MUI2默认不会自动创建目录，electron-builder有这个功能，我们也要加上。可以参考刚刚的electron-builder的源码。

   ```nsis
   ; StrContains
   ; nsis官方提供的函数，https://nsis.sourceforge.io/StrContains，需编码进脚本文件
   ; 用于识别路径是否包含应用目录
    
   Var STR_HAYSTACK
   Var STR_NEEDLE
   Var STR_CONTAINS_VAR_1
   Var STR_CONTAINS_VAR_2
   Var STR_CONTAINS_VAR_3
   Var STR_CONTAINS_VAR_4
   Var STR_RETURN_VAR
    
   Function StrContains
     Exch $STR_NEEDLE
     Exch 1
     Exch $STR_HAYSTACK
     ; Uncomment to debug
     ;MessageBox MB_OK 'STR_NEEDLE = $STR_NEEDLE STR_HAYSTACK = $STR_HAYSTACK '
       StrCpy $STR_RETURN_VAR ""
       StrCpy $STR_CONTAINS_VAR_1 -1
       StrLen $STR_CONTAINS_VAR_2 $STR_NEEDLE
       StrLen $STR_CONTAINS_VAR_4 $STR_HAYSTACK
       loop:
         IntOp $STR_CONTAINS_VAR_1 $STR_CONTAINS_VAR_1 + 1
         StrCpy $STR_CONTAINS_VAR_3 $STR_HAYSTACK $STR_CONTAINS_VAR_2 $STR_CONTAINS_VAR_1
         StrCmp $STR_CONTAINS_VAR_3 $STR_NEEDLE found
         StrCmp $STR_CONTAINS_VAR_1 $STR_CONTAINS_VAR_4 done
         Goto loop
       found:
         StrCpy $STR_RETURN_VAR $STR_NEEDLE
         Goto done
       done:
      Pop $STR_NEEDLE ;Prevent "invalid opcode" errors and keep the
      Exch $STR_RETURN_VAR  
   FunctionEnd
    
   !macro _StrContainsConstructor OUT NEEDLE HAYSTACK
     Push `${HAYSTACK}`
     Push `${NEEDLE}`
     Call StrContains
     Pop `${OUT}`
   !macroend
    
   !define StrContains '!insertmacro "_StrContainsConstructor"'
   
   ; 在安装前（选择数据目录点击下一步时）修改应用安装路径
   Function CustomFunctionLeave
       ; 数据目录也要做校验
       ${StrContains} $0 "HelloElectron" $DataLocation
   	${If} $0 == ""
   	StrCpy $INSTDIR "$DataLocation\HelloElectron"
   	${endIf}
           
   	${StrContains} $0 "HelloElectron" $INSTDIR
   	${If} $0 == ""
   	StrCpy $INSTDIR "$INSTDIR\HelloElectron"
   	${endIf}
   FunctionEnd
   ```

2. 定制数据页面

   修改页面UI，增加目录和选择

   ```nsis
   Var DataLocationDir
   Var DataLocationText
   !macro preInit
       StrCpy $DataLocationText "C:/ProgramData/HelloElectron"
   !macroend
   
   Function CustomFunction
       !insertmacro MUI_HEADER_TEXT_PAGE "选择数据位置" "选择数据要安装的文件夹"   ; 定制标头
   
       ; 创建页面UI
       nsDialogs::Create /NOUNLOAD 1018
       pop $0
   
       ; 创建标签控件
       ${NSD_CreateLabel} 0 0 100% 12u "选择数据目录"
       Pop $0
   
       ; 创建目录框 四个数字代表：长 上边距 宽 高
       ${NSD_CreateDirRequest} 0 15u 70% 12u "$DataLocationText"
       Pop $DataLocationDir
   
       ; 创建浏览按钮
       ${NSD_CreateBrowseButton} 72% 13u 20% 15u "浏览(B)..."
       Pop $0
   
       ; 设置浏览函数
       ${NSD_OnClick} $0 BrowseButtonClick
       Pop $0
   
       ; 显示页面
       nsDialogs::show
   FunctionEnd
   ```

   页面有了，给按键增加功能：

   ```nsis
   Function BrowseButtonClick
       nsDialogs::SelectFolderDialog "选择数据位置" "$DataLocationText"
       Pop $0
   
       ${if} $0 != "error"
           ${NSD_SetText} $DataLocationDir $0
       ${Endif}
   FunctionEnd
   
   ; 在安装前（选择数据目录点击下一步时）修改应用安装路径
   Function CustomFunctionLeave
       ${NSD_GetText} $DataLocationDir $DataLocationText			; 从目录框获取文本
   
       ; 数据目录也要做校验
       ${StrContains} $0 "HelloElectron" $DataLocationText
   	${If} $0 == ""
   	StrCpy $DataLocationText "$DataLocationText\HelloElectron"
   	${endIf}
           
   	${StrContains} $0 "HelloElectron" $INSTDIR
   	${If} $0 == ""
   	StrCpy $INSTDIR "$INSTDIR\HelloElectron"
   	${endIf}
   FunctionEnd
   ```

3. 数据持久化

   如果需要将数据做持久化，可以考虑写入文件或写入注册表

   ```nsis
   !macro customInstall
       ; 写文件
       Delete "$INSTDIR\output.ini"                ; 删除做初始化
       FileOpen $0 "$INSTDIR\output.ini" "w"       ; 打开一个文件用于写入
       FileWrite $0 "$DataLocationText"            ; 写入文本
       FileClose $0                                ; 关闭文件
   
       ; 写注册表
       SetRegView 32
       WriteRegStr HKLM "${INSTALL_REGISTRY_KEY}" DataLocation $DataLocationText
       SetRegView 64
       WriteRegStr HKLM "${INSTALL_REGISTRY_KEY}" DataLocation $DataLocationText
   !macroend
   ```

# 文件打包

```
npm run make
```





